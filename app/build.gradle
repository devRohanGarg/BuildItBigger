apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 10
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.0'
    // Added for jokeViewer library
    compile project(':jokeViewer')
    // Added for loading indicator button
    compile project(':progressButton')
    // Added for AdMob
    freeCompile 'com.google.android.gms:play-services-ads:9.4.0'
    compile project(path: ':backend', configuration: 'android-endpoints')
    // Set this dependency to build and run Espresso tests
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support:support-annotations:24.2.0'
}

/*
Gradle task that:

1) Launches the GCE local development server
2) Runs all tests
3) Shuts the server down again
*/
task testProject {
    dependsOn(['startDevServer', 'runTests', 'shutdownDevServer'])
}

task startDevServer {
    dependsOn ':backend:appengineRun'
    evaluationDependsOn(":backend")
    project(':backend').appengine.daemon = true
}

task runTests {
    dependsOn ':app:connectedAndroidTest'
    mustRunAfter "startDevServer"
}

task shutdownDevServer {
    dependsOn ':backend:appengineStop'
    mustRunAfter "runTests"
}